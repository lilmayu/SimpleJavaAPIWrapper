plugins {
    id 'java'
    id 'java-library'
    id 'signing'
    id 'maven-publish'
    id "com.github.johnrengelman.shadow" version "8.1.1"
    id "io.github.gradle-nexus.publish-plugin" version "1.1.0"
}

group 'dev.mayuna'
version '2.3.1'

repositories {
    mavenCentral()
}

dependencies {
    // GSON
    compileOnly 'com.google.code.gson:gson:2.11.0'
    testImplementation 'com.google.code.gson:gson:2.11.0'

    // Jetbrains annotations
    implementation 'org.jetbrains:annotations:24.0.0'

    // Lombok
    compileOnly 'org.projectlombok:lombok:1.18.+'
    annotationProcessor 'org.projectlombok:lombok:1.18.+'
    testCompileOnly 'org.projectlombok:lombok:1.18.+'
    testAnnotationProcessor 'org.projectlombok:lombok:1.18.+'

    // JUnit
    testImplementation 'junit:junit:4.13.2'
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.8.2'
    testRuntimeOnly("org.junit.vintage:junit-vintage-engine:5.8.2")
}


// == Quick tasks == //

task publishCloseAndRelease() {
    dependsOn 'publishToSonatype'
    dependsOn 'closeAndReleaseSonatypeStagingRepository'
}

shadowJar {

}

test {
    useJUnitPlatform()
}

tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
}

test {
    useJUnitPlatform();
}

// == Maven publishing == //

publishing {

    publications {
        shadow(MavenPublication) {
            groupId = 'dev.mayuna'
            artifactId = 'simple-java-api-wrapper'
            version = getVersion()
            from components.java

            pom {
                name = 'simple-java-api-wrapper'
                description = 'Super lightweight framework for easier API wrapping made in Java'
                url = 'https://mayuna.dev/'

                scm {
                    connection = 'scm:https://github.com/lilmayu/SimpleJavaAPIWrapper'
                    developerConnection = 'scm:git:https://github.com/lilmayu/SimpleJavaAPIWrapper.git'
                    url = 'https://github.com/lilmayu/SimpleJavaAPIWrapper'
                }

                licenses {
                    license {
                        name = 'Apache License 2.0'
                        url = 'https://www.apache.org/licenses/LICENSE-2.0'
                    }
                }

                developers {
                    developer {
                        id = 'mayuna'
                        name = 'Marek Lof'
                        email = 'uwulilmayu@gmail.com'
                    }
                }
            }
        }
    }

    publishing {
        repositories {
            maven {
                credentials {
                    username = project.properties['ossrhUsername'] ?: System.getenv('ossrhUsername')
                    password = project.properties['ossrhPassword'] ?: System.getenv('ossrhPassword')
                }

                url = "https://s01.oss.sonatype.org/service/local/staging/deploy/maven2/"
            }
        }
    }
}

signing {
    def signingKey = findProperty("signingKey")
    def signingPassword = findProperty("signingPassword")
    useInMemoryPgpKeys(signingKey, signingPassword)
    sign publishing.publications.shadow
}

nexusPublishing {
    repositories {
        sonatype {
            nexusUrl.set(uri("https://s01.oss.sonatype.org/service/local/"))
            snapshotRepositoryUrl.set(uri("https://s01.oss.sonatype.org/content/repositories/snapshots/"))
            username = project.properties['ossrhUsername'] ?: System.getenv('ossrhUsername')
            password = project.properties['ossrhPassword'] ?: System.getenv('ossrhPassword')
        }
    }
}

java {
    withJavadocJar()
    withSourcesJar()
}

shadowJar.dependsOn javadocJar
shadowJar.dependsOn sourcesJar
shadowJar.dependsOn jar

components.java.withVariantsFromConfiguration(configurations.shadowRuntimeElements) {
    skip()
}
